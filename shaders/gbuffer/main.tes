#version 430 core

#include "/lib/constants.glsl"
#include "/settings.glsl"

layout (triangles, fractional_even_spacing, ccw) in;

#include "/lib/common.glsl"

in VertexData2 {
    vec2 uv;
    vec2 light;
    vec4 color;
    vec3 localPos;
    vec3 localOffset;
    vec3 localNormal;
    vec4 localTangent;
    flat uint blockId;

    vec3 surfacePos;

	#ifdef RENDER_PARALLAX
		vec3 tangentViewPos;
		flat vec2 atlasMinCoord;
		flat vec2 atlasMaxCoord;
	#endif
} vIn[];

out VertexData2 {
    vec2 uv;
    vec2 light;
    vec4 color;
    vec3 localPos;
    vec3 localOffset;
    vec3 localNormal;
    vec4 localTangent;
    flat uint blockId;

    vec3 surfacePos;

	#ifdef RENDER_PARALLAX
		vec3 tangentViewPos;
        flat vec2 atlasMinCoord;
        flat vec2 atlasMaxCoord;
	#endif
} vOut;

#include "/lib/water_waves.glsl"

#include "/lib/utility/tessellation_eval.glsl"

#ifdef EFFECT_TAA_ENABLED
    #include "/lib/taa_jitter.glsl"
#endif


void main() {
    gl_Position = _interpolate(gl_in[0].gl_Position, gl_in[1].gl_Position, gl_in[2].gl_Position);

    vOut.uv = _interpolate(vIn[0].uv, vIn[1].uv, vIn[2].uv);
    vOut.light = _interpolate(vIn[0].light, vIn[1].light, vIn[2].light);
    vOut.color = _interpolate(vIn[0].color, vIn[1].color, vIn[2].color);
    vOut.localPos = _interpolate(vIn[0].localPos, vIn[1].localPos, vIn[2].localPos);
    vOut.localOffset = _interpolate(vIn[0].localOffset, vIn[1].localOffset, vIn[2].localOffset);
    vOut.localNormal = _interpolate(vIn[0].localNormal, vIn[1].localNormal, vIn[2].localNormal);
    vOut.localTangent = _interpolate(vIn[0].localTangent, vIn[1].localTangent, vIn[2].localTangent);
    vOut.surfacePos = _interpolate(vIn[0].surfacePos, vIn[1].surfacePos, vIn[2].surfacePos);

    vOut.blockId = vIn[0].blockId;

    #ifdef RENDER_PARALLAX
        vOut.tangentViewPos = _interpolate(vIn[0].tangentViewPos, vIn[1].tangentViewPos, vIn[2].tangentViewPos);

        vOut.atlasMinCoord = vIn[0].atlasMinCoord;
        vOut.atlasMaxCoord = vIn[0].atlasMaxCoord;
    #endif

    // bool isWater = bitfieldExtract(vIn[0].blockId, 6, 1) != 0;
    bool is_fluid = iris_hasFluid(vIn[0].blockId);

    if (is_fluid) {
        float distF = 1.0 - smoothstep(0.2, 2.8, length(vOut.surfacePos));
        distF = 1.0 - (distF*distF);
        
        vec3 waveOffset = GetWaveHeight(ap.camera.pos + vOut.localPos, vOut.light.y, ap.frame.time, WaterWaveOctaveMin);
        vOut.localPos.y += distF * waveOffset.y;

        vec3 viewPos = mul3(ap.camera.view, vOut.localPos);
        gl_Position = ap.camera.projection * vec4(viewPos, 1.0);

        #ifdef EFFECT_TAA_ENABLED
            jitter(gl_Position);
        #endif
    }
}
